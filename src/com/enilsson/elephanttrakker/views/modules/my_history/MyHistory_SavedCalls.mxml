<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:en="com.enilsson.controls.*"
	xmlns:graphics="com.enilsson.graphics.*" 
	xmlns:common="com.enilsson.elephanttrakker.views.common.*"
	show="init()">

	<mx:DataGrid id="savedDG" dataProvider="{_model.my_history.user_storage}" 
		width="100%" height="{this.height - 41}" 
		top="5" selectable="true" verticalGridLines="false" horizontalScrollPolicy="off" 
		headerRelease="sortAction(event)">
		<mx:columns>
            <mx:DataGridColumn textAlign="left" dataField="lname" headerText="Last Name" fontWeight="bold"  />		
            <mx:DataGridColumn textAlign="left" dataField="fname" headerText="First Name" fontWeight="bold"  />
            <mx:DataGridColumn textAlign="left" dataField="address1" headerText="Address"  />
            <mx:DataGridColumn textAlign="left" dataField="city" headerText="City" />
            <mx:DataGridColumn textAlign="left" dataField="state" headerText="State" width="40"  />
            <mx:DataGridColumn textAlign="left" dataField="pledge_amount" headerText="Amount" 
            	width="65" fontWeight="bold" labelFunction="money_labelFunc"  /> 
            <mx:DataGridColumn textAlign="left" dataField="modified_on_plain" headerText="Date" />           
            <mx:DataGridColumn textAlign="left" width="60" headerText="Actions" 
            	itemRenderer="com.enilsson.elephanttrakker.views.modules.my_history.renderers.MyHistory_SavedCallsActions" />
        </mx:columns>
	</mx:DataGrid>
	
	<common:DataGridFooter id="dgFooter" bottom="0" showSearchBox="false" 
		searchStart="handleSearch()" newPage="newPageAction()" clearSearch="clearSearchAction()"
		itemsPerPage="{_model.itemsPerPage}" itemsTotal="{_model.my_history.numSavedCalls}" />


	<mx:CurrencyFormatter id="currencyFormatter" precision="0" currencySymbol="$" useNegativeSign="false" />
	<mx:DateFormatter id="dateFormatter" formatString="MM/DD/YY" />

	
	<mx:Script>
		<![CDATA[
			import com.enilsson.elephanttrakker.vo.SearchVO;
			import com.enilsson.elephanttrakker.events.modules.my_history.MyHistoryGetSavedCallsEvent;
			import com.enilsson.elephanttrakker.events.modules.my_history.MyHistorySearchEvent;
			import org.osflash.thunderbolt.Logger;
			import com.enilsson.elephanttrakker.events.modules.my_history.MyHistoryGetContribsEvent;
			import mx.events.DataGridEvent;
			import mx.events.ItemClickEvent;
			import mx.messaging.AbstractConsumer;
			import com.enilsson.elephanttrakker.models.ETModelLocator;
			
			[Bindable] private var _model:ETModelLocator = ETModelLocator.getInstance();
			private var sortActions:Object = new Object();			
			private var lastSort:String = 'user_storage.created_on DESC';
			
			public function init():void
			{
				if(_model.debug){ Logger.info('myHistory Contribs Init'); }
				
				if( _model.session && _model.my_history.initSavedCalls)
				{
					_model.my_history.initSavedCalls = false;
					new MyHistoryGetSavedCallsEvent(0, _model.itemsPerPage, 'P', lastSort).dispatch();
				}
			}
			
			private function handleSearch():void
			{
				if(_model.debug) Logger.info('Start Search', dgFooter.searchTerm);
				
				new MyHistorySearchEvent( 
					new SearchVO( 'user_storage', dgFooter.searchTerm, null, 0, 200, null, true )
				).dispatch();
				dgFooter.reset();
			}
			
			private function newPageAction():void
			{
				if(_model.debug) Logger.info('New Page', dgFooter.selectedPage);

				new MyHistoryGetSavedCallsEvent(
					dgFooter.selectedPage * _model.itemsPerPage, 
					_model.itemsPerPage, 
					'', 
					lastSort
				).dispatch();
			}
			
			private function clearSearchAction():void
			{
				if(_model.debug) Logger.info('Clear Search');
				
				new MyHistoryGetSavedCallsEvent(0, _model.itemsPerPage, 'P', lastSort).dispatch();				
			}
			
			private function sortAction(e:DataGridEvent):void
			{
				if (_model.my_history.isSorting) {
					e.preventDefault();
					return;
				}

				_model.my_history.isSorting = true;
				
				if(_model.debug) Logger.info('SortAction', e.dataField);
			 	
			 	// stop the default sort action
			 	e.preventDefault();
			 	// get the fieldname
			 	var fieldName:String = e.dataField;
			 	var sort:String = '';
			 	// loop through and highlight the sorted column
			 	for ( var i:int=0; i<savedDG.columns.length; i++)
			 	{
			 		if(e.columnIndex == i)	{
			 			savedDG.columns[i].setStyle('backgroundColor','#ededed');
			 		} else {
			 			savedDG.columns[i].setStyle('backgroundColor','none');
			 		}
			 	}
			 	// run the sort action, desc first or toggle the existing
			 	if(sortActions[fieldName])
			 	{
			 		var direction:String = sortActions[fieldName] == 'ASC' ? 'DESC' : 'ASC';
			 		sortActions[fieldName] = direction;
			 		sort = 'user_storage.' + fieldName + ' ' + direction;	
			 		lastSort = sort;		 		
			 		new MyHistoryGetSavedCallsEvent(0, _model.itemsPerPage, 'P', sort).dispatch();
			 		dgFooter.reset();			 		
			 	} 
			 	else 
			 	{
			 		sortActions[fieldName] = 'DESC';
			 		sort = 'user_storage.' + fieldName + ' DESC';
			 		lastSort = sort;
			 		new MyHistoryGetSavedCallsEvent(0, _model.itemsPerPage, 'P', sort).dispatch();
			 		dgFooter.reset();
			 	}
			}
			
			/**
			 * Label function to show the dollars in the correct format
			 */
			private function money_labelFunc(item:Object, column:DataGridColumn):String {
		  		return currencyFormatter.format(item[column.dataField]);
			} 

		]]>
	</mx:Script>

</mx:Canvas>
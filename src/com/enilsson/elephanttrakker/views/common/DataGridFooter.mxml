<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:controls="com.enilsson.controls.*" 
	xmlns:graphics="com.enilsson.graphics.*" 	
	height="36" width="100%"
	backgroundColor="#ededed" 
	borderSides="right,left,bottom" borderColor="#999999" borderStyle="solid" >
	
	<mx:Metadata>
		[Event(name="searchStart", type="flash.events.Event")]
		[Event(name="clearSearch", type="flash.events.Event")]		
		[Event(name="newPage", type="flash.events.Event")]
		[Event(name="changePage", type="flash.events.Event")]				
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import mx.utils.StringUtil;
			import org.osflash.thunderbolt.Logger;
			import com.enilsson.elephanttrakker.models.ETModelLocator;
			
			[Bindable] private var _model:ETModelLocator = ETModelLocator.getInstance();
			[Bindable] private var _itemsPerPage:int = 0;
			[Bindable] private var _itemsTotal:int = 0;
			[Bindable] private var _showSearchBox:Boolean = true;
			[Bindable] private var _searchFn:Function;
			[Bindable] private var _newPage:*;
			
			private var _currentPage : int = -1;			
			private var _searchTerm : String = "";
			
			public function set itemsPerPage(value:int):void
			{
				if(value == _itemsPerPage) return;
				
				if(_model.debug) Logger.info('Datagrid Footer - Set ItemsPerPage');
				
				_itemsPerPage = value;
				setText()				
			}
			public function get itemsPerPage():int
			{
				return _itemsPerPage;
			}	

			public function set showSearchBox(value:Boolean):void
			{
				_showSearchBox = value;
			}
						
			public function set itemsTotal(value:int):void
			{
				if(value == _itemsTotal) return; 
				
				if(_model.debug) Logger.info('Datagrid Footer - Set ItemsTotal');
				
				_itemsTotal = value;
				setText()				
			}
			public function get itemsTotal():int
			{
				return _itemsTotal;
			}	
			
			public function get searchTerm():String
			{
				return txtFilter.text;	
			}	
			
			public function get selectedPage():int
			{
				return paginator.selectedIndex;
			}	
			
			public function reset():void
			{
				if(_model.debug) Logger.info('Datagrid Footer - Reset');
				
				paginator.selectedIndex = 0;
				setText();				
			}
			
			public function get showSearchBox():Boolean
			{
				return _showSearchBox;	
			}				
			
			private function txtFilterKeyHandler(event:KeyboardEvent):void
			{
				//only dispatch a search event if the user entered more than just whitespaces into the textfield
				if(event.keyCode == Keyboard.ENTER && StringUtil.trim(txtFilter.text).length > 0)
				{
					_searchTerm = txtFilter.text;
					dispatchEvent( new Event( 'searchStart', true ) );
				}				
			}
			
			private function clearSearchAction():void
			{
				txtFilter.text = '';
				
				// only dispatch a clear event if there was more than just whitespaces in the current seach term. 
				if(_searchTerm.length > 0)
				{
					_searchTerm = "";
					dispatchEvent( new Event( 'clearSearch', true ) );
				}
			}
			
			private function newPageHandler():void
			{
				if(_currentPage != selectedPage)
				{
					_currentPage = selectedPage;
					
					if(_model.debug) Logger.info('Datagrid Footer - New Page');
				
					dispatchEvent( new Event( 'changePage', true ) );
				}
			}
			
			private function setText():void
			{
				if(_model.debug) Logger.info('Datagrid Footer - Set Text', _itemsTotal, _itemsPerPage, paginator.selectedIndex);
				
				var plural:String = _itemsTotal > 1 ? 's' : '';				
				
				if(_itemsTotal < _itemsPerPage)
				{
					desc.htmlText = '<b>' + _itemsTotal + '</b> record' + plural;
				}
				else
				{
					var startIndex:String = (paginator.selectedIndex*_itemsPerPage+1).toString();
					var toIndex:int =  paginator.selectedIndex * _itemsPerPage + _itemsPerPage;
					var toIndexString:String = toIndex < _itemsTotal ? toIndex.toString() : _itemsTotal.toString();
					
					desc.htmlText = 'showing <b>' + startIndex + '</b>';
					desc.htmlText += '- <b>' + toIndexString + '</b>';
					desc.htmlText += ' of <b>' + _itemsTotal + '</b> record' + plural;				
				}
			}
		]]>
	</mx:Script>
	
	<mx:Canvas top="6" left="7" visible="{_showSearchBox}">
		<mx:TextInput id="txtFilter" styleName="txtFilter" 
			width="140" height="20"
			top="2" left="0" 			
			keyUp="txtFilterKeyHandler(event)"
			cornerRadius="3" borderColor="#666666" borderStyle="solid" backgroundColor="#FFFFFF"  
			fontSize="9" paddingLeft="20" />
		<mx:Image source="@Embed('/assets/images/magnifier.png')" left="4" top="4" />
		<graphics:enCloseBtn id="clearSearch" 
			visible="{txtFilter.text.length > 0}" 
			toolTip="Clear Search" 
			borderColor="#ededed" 
			click="clearSearchAction()"
			top="3" left="120" 
			size="18" 
			dropShadow="false" 
			backgroundColor="#666666" />
	</mx:Canvas>		
	
	<mx:Text id="desc" 
		verticalCenter="0" horizontalCenter="0" 
		fontSize="10" color="#333333" 
		visible="{_itemsTotal > 0}" />
	
	<controls:Paginator id="paginator" styleName="paginator" 
		debugMode="{_model.debug}"
		verticalCenter="0" right="10" 
		rangeCount="3" buttonHeight="18" 
		newPage="newPageHandler()" 
		selectedIndexChanged="setText()"
		visible="{_itemsTotal > _itemsPerPage}"
		itemsPerPage="{_itemsPerPage}" 
		itemsTotal="{_itemsTotal}" />
		
</mx:Canvas>
<?xml version="1.0" encoding="utf-8"?>
<AdvancedList 
	xmlns="com.flextoolbox.controls.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	itemRenderer="com.enilsson.elephantadmin.views.modules.batch.renderers.CheckWrapperRenderer"
	dragEnabled="true" dropEnabled="true" dragMoveEnabled="false" allowMultipleSelection="true"
	dropIndicatorSkin="{ProgrammaticSkin}"
	enabledFunction="{isCheckEnabled}"
	dragDrop="handleDragDrop(event)"
	dragEnter="handleDragEnter(event)"
	dragComplete="handleDragComplete(event)"
	creationComplete="handleCreationComplete()">
	
	<mx:Script>
		<![CDATA[
			import com.enilsson.elephantadmin.views.modules.batch.renderers.CheckWrapper;
			import mx.core.IDataRenderer;
			import com.enilsson.elephantadmin.views.modules.batch.renderers.CheckWrapperRenderer;
			import mx.core.DragSource;
			import mx.events.DragEvent;
			import com.enilsson.elephantadmin.events.modules.batch.BatchListEvent;
			import mx.skins.ProgrammaticSkin;
			
			private function handleCreationComplete() : void {
				addEventListener( CheckWrapperRenderer.ADD_CHECK, handleAddCheckToBatch );
			}
			
			private function isCheckEnabled( item : Object ) : Boolean {
				return ( ! item.inBatch );
			}
			
			private function addChecksToNewBatch( checks : Array ) : void {
				new BatchListEvent( BatchListEvent.ADD_CHECKS_TO_NEW_BATCH, checks ).dispatch();
				selectedItems = [];
			}

			private function removeChecksFromBatch( checks : Array ) : void {
				new BatchListEvent( BatchListEvent.REMOVE_CHECKS_FROM_NEW_BATCH, checks ).dispatch();
			}
			
			private function handleDragComplete( event : DragEvent ) : void {
				selectedItems = [];
			}
			
			private function handleDragDrop(event:DragEvent):void
			{
				removeChecksFromBatch(event.dragSource.dataForFormat("items") as Array);
				event.dragSource = new DragSource();
			}
			
			private function handleDragEnter( event : DragEvent ) : void {
				if( event.dragInitiator == event.currentTarget )
					event.dragSource = new DragSource();
			}
			
			private function handleAddCheckToBatch( event : Event ) : void {
				
				event.stopPropagation();
				
				var c : Object = IDataRenderer(event.target).data;				
				var items : Array = [];								
				
				items = items.concat(selectedItems);
								
				if( items.indexOf(c) == -1)
					items.push(c);
				
				addChecksToNewBatch( items );
			}
		]]>
	</mx:Script>

	
</AdvancedList>

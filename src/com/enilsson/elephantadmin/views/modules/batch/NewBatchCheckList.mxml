<?xml version="1.0" encoding="utf-8"?>
<mx:List xmlns:mx="http://www.adobe.com/2006/mxml"
	dropEnabled="true" dragEnabled="true" dragMoveEnabled="false" allowMultipleSelection="true"
	dropIndicatorSkin="{ProgrammaticSkin}"
	dragDrop="handleDragDrop(event)"
	dragEnter="handleDragEnter(event)"
	dragComplete="handleDragComplete(event)"
	itemRenderer="com.enilsson.elephantadmin.views.modules.batch.renderers.NewBatchCheckRenderer"
	creationComplete="handleCreationComplete()">
	
	<mx:Script>
		<![CDATA[
			import mx.core.IDataRenderer;
			import com.enilsson.elephantadmin.views.modules.batch.renderers.NewBatchCheckRenderer;
			import mx.skins.ProgrammaticSkin;
			import com.enilsson.elephantadmin.events.modules.batch.BatchListEvent;
			import mx.core.DragSource;
			import mx.events.DragEvent;
			
			override protected function mouseDownHandler(event:MouseEvent):void
			{
				super.mouseDownHandler(event);
			}
			
			private function handleDragEnter( event : DragEvent ) : void 
			{
				if( event.dragInitiator == event.currentTarget ) {
					event.dragSource = new DragSource();
				}	
			}
			
			private function handleDragComplete( event : DragEvent ) : void {
				selectedItems = [];
			}
			
			public function handleDragDrop(event:DragEvent):void
			{
				if(event.currentTarget != event.dragInitiator) {
					addChecksToBatch( event.dragSource.dataForFormat("items") as Array );
					event.dragSource = new DragSource();
				}
			}
			
			private function handleCreationComplete() : void {
				addEventListener( NewBatchCheckRenderer.REMOVE_CHECK, handleRemoveCheck );
			}
			
			private function removeChecksFromNewBatch( checks : Array) : void {
				new BatchListEvent( BatchListEvent.REMOVE_CHECKS_FROM_NEW_BATCH, checks ).dispatch();
				selectedItems = [];
			}
			
			private function handleRemoveCheck( event : Event ) : void {
				event.stopPropagation();
				
				var c : Object = IDataRenderer(event.target).data;				
				var items : Array = [];								
				
				items = items.concat(selectedItems);
								
				if( items.indexOf(c) == -1)
					items.push(c);
				
				removeChecksFromNewBatch( items );
			}
			
			public function addChecksToBatch( checks : Array ) : void
			{
				new BatchListEvent( BatchListEvent.ADD_CHECKS_TO_NEW_BATCH, checks ).dispatch();
			}
			
		]]>
	</mx:Script>
	
</mx:List>

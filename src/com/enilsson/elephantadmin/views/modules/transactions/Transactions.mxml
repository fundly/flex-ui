<?xml version="1.0" encoding="utf-8"?>
<common:LabeledContainer
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:common="com.enilsson.elephantadmin.views.common.*" 
	xmlns:transactions="com.enilsson.elephantadmin.views.modules.transactions.*"
	xmlns:popups="com.enilsson.elephantadmin.views.modules.transactions.popups.*"
	label="{_model.viewStateNames.getItemAt(moduleID)}" >
	
	<mx:HBox horizontalGap="10" width="100%" height="100%">
		<common:ModuleGrid id="moduleGrid"
			height="100%" width="100%"
			minWidth="585"
			dataProvider="{_model.transactions.records}"
			searchComboList="{_model.transactions.searchBoxCombo}"
			listItemClick="listClickHandler()" 
			searchStart="searchStart(event)"
			indexSearch="indexSearchStart(event)"
			clearSearch="resetSearch()" 
			newPage="newPageAction(event)"
			showDateFields="true"
			columns="{columns}"
			fromDateChange="fromDateChange(event)"
			toDateChange="toDateChange(event)"
			selectedIndex="{_model.transactions.selectedIndex}" 
			selectedPage="{_model.transactions.currPage / _model.itemsPerPage}" 
			itemsPerPage="{_model.itemsPerPage}" 
			itemsTotal="{_model.transactions.totalRecords}"
			showExportButton="true"
			exportClick="handleExportClick(event)"
			/>
		<transactions:Transactions_Detail id="transactionDetail"
			dataProvider="{_model.transactions.details}"
			width="350" 
			height="100%"
			showDetails="showSuccessDetails.message = event.currentTarget.response" />
	</mx:HBox>
	
	<popups:ShowTransactionDetails 
		id="showSuccessDetails" 
		title="Transaction Details"
		type="successPanel" />

	<mx:CurrencyFormatter id="currencyFormat"
		currencySymbol="$"
		decimalSeparatorFrom="."
		decimalSeparatorTo="."
		precision="2"
		rounding="nearest"
		thousandsSeparatorFrom=","
		thousandsSeparatorTo=","
		useThousandsSeparator="true"
		/>
	<mx:DateFormatter id="dateFormat" formatString="MM/DD/YYYY LL:NNA" />

	<mx:Array id="columns">
		<mx:DataGridColumn headerText="Transaction ID" dataField="transactionid" width="90"/>
		<mx:DataGridColumn headerText="Amount" labelFunction="amountLabel" sortCompareFunction="numberSort" width="50" textAlign="right"/>
		<mx:DataGridColumn headerText="Name on Card" dataField="full_name" width="150" />
		<mx:DataGridColumn headerText="Card Type" dataField="card_number_type" width="60" />
		<mx:DataGridColumn headerText="Card Number" dataField="card_number" width="100" />
		<mx:DataGridColumn headerText="Engine" dataField="engine" width="75" />
		<mx:DataGridColumn headerText="Date" dataField="created_on" labelFunction="date_labelFunc" sortCompareFunction="timeSort" width="100"/>
		<mx:DataGridColumn width="22">
			<mx:itemRenderer>
				<mx:Component>
					<mx:HBox horizontalAlign="center">
						<mx:Button useHandCursor="true" buttonMode="true"
							styleName="inspectRecord"
							click="new SidEvent ( data.pledge_id.sid ).dispatch()"
							toolTip="View the parent pledge" />								
						<mx:Script>
							<![CDATA[
								import com.enilsson.elephantadmin.events.main.SidEvent;
							]]>
						</mx:Script>					
					</mx:HBox>
				</mx:Component>
			</mx:itemRenderer>
		</mx:DataGridColumn>	
	</mx:Array>

	<mx:Script>
		<![CDATA[
			import com.enilsson.utils.EDateUtil;
			import com.enilsson.elephantadmin.vo.SidVO;
			import mx.formatters.DateFormatter;
			import mx.utils.ObjectUtil;
			import mx.collections.ArrayCollection;

			import com.enilsson.elephantadmin.models.EAModelLocator;
			import com.enilsson.elephantadmin.events.modules.TransactionsEvent;
			import com.enilsson.elephantadmin.vo.RecordsVO;
			import com.enilsson.elephantadmin.vo.RecordVO;
			import com.enilsson.elephantadmin.vo.SearchVO;
			
			import org.osflash.thunderbolt.Logger;
			
			[Bindable] private var _model:EAModelLocator = EAModelLocator.getInstance();

			override protected function init():void
			{
				super.init();

				// grab any SID data if present
				var sid:SidVO = _model.sid
			
				if(sid)
				{
					if(sid.table_name == 'transactions')
					{
						_model.transactions.details = sid.data;
						_model.transactions.sidRecord = true;
						_model.sid = null;
					}
				}

				// get default list
				resetSearch();
				
				// reset the data changed flag
				_model.dataChanged = false;
			}
			
			private function dispatchTransactionEvent( whereObj : Object ) : void {
				
				if( ! whereObj ) whereObj = {};
				
				new TransactionsEvent( 
						TransactionsEvent.TRANSACTIONS_RECORDS, { 
							'recordsVO' : new RecordsVO (
								'transactions(pledge_id(contact_id),created_by_id<fname:lname:_fid>)',
								whereObj, 
								'transactions.created_on DESC', 
								_model.transactions.currPage, 
								_model.itemsPerPage, 
								'P' 
							)
						}
					).dispatch();
			}
			
			private function buildWhere( index : uint, part : Object ) : Object 
			{							
				var indexStr : String = "(" + index + ")";
								
				var whereObj 	: Object;
				var whereArray	: Array = [];
				
				try {
					whereObj = (_model.transactions.lastQuery.params.recordsVO as RecordsVO).where;	
				}
				catch( e : Error ) { }
				
				
				if( whereObj && part )
				{
					whereArray = String(whereObj.statement).split(" AND ");
					
					if( whereArray.indexOf( indexStr ) == -1)	// if condition does not exist add to the where statement
						whereArray.push( indexStr );
					whereObj['statement'] = whereArray.join(" AND ");
				} 
				else 
				{
					whereObj = new Object();
					whereObj['statement'] = indexStr;
				}
				
				if( part )
					whereObj[index] = part;
				
				// filter out transactions with transaction_refids
				if( whereArray.indexOf( '(1)' ) == -1 ) {
					whereArray.push( '(1)' );
					whereObj['statement'] = whereArray.join( " AND " );
					whereObj[1] = {
						what:	'transactions.transactions_refid',
						op: 	'IS',
						val: 	'NULL'
					};
				}
				
				return whereObj;
			}
			
									
			private function searchStart(event:Event):void 
			{
				if(moduleGrid.searchTerm != ""){
					moduleGrid.clearSearch.visible = true;

					var searchField:String = moduleGrid.searchOption.data;

					var wherePart : Object = { 
						'what' : searchField,
						'val' : "%"+moduleGrid.searchTerm+"%",
						'op' : 'LIKE'
					};
	
					var where : Object = buildWhere( 2, wherePart );
					dispatchTransactionEvent( where );
				}
			}
			
			private function indexSearchStart(event:Event):void 
			{
				moduleGrid.clearSearch.visible = true;
				
				var wherePart : Object = { 
					'what' : 'transactions.full_name',
					'val' : moduleGrid.indexBar.searchLetter+"%",
					'op' : 'LIKE'
				};
	
				var where : Object = buildWhere( 2, wherePart );
				dispatchTransactionEvent( where );
			}
			
			private function resetSearch():void 
			{
				moduleGrid.clearSearch.visible = false;
				moduleGrid.clearDateRange();
				_model.transactions.currPage = 0;
				
				var where : Object = buildWhere( 2, null );
				dispatchTransactionEvent( where );
			}
			
			private function newPageAction(event:Event):void 
			{
				_model.transactions.currPage = moduleGrid.selectedPage * _model.itemsPerPage;
				switch(_model.transactions.lastQuery.type){
					case TransactionsEvent.TRANSACTIONS_RECORDS:
						RecordsVO(_model.transactions.lastQuery.params.recordsVO).iFrom = _model.transactions.currPage;
						_model.transactions.lastQuery.dispatch();
 						break;
				}
			}

			private function getFromDateWhere() : Object {
				return { 
							'what' : 'transactions.created_on',
							'op' : '>',
							'val' : EDateUtil.localDateToTimestamp(moduleGrid.fromDate ) //start of the date range
						};
			}
			private function getToDateWhere() : Object {
				return {	
							'what' : 'transactions.created_on',
							'op' : '<',
							'val' : EDateUtil.localDateToTimestamp(moduleGrid.toDate) + 24*60*60 // end of the date range
						};
			}
			
			private function fromDateChange(event:Event):void 
			{
				moduleGrid.clearSearch.visible = true;

				switch(_model.transactions.lastQuery.type){
					case TransactionsEvent.TRANSACTIONS_RECORDS:
						var where : Object = buildWhere( 3, getFromDateWhere() );
												
						RecordsVO(_model.transactions.lastQuery.params.recordsVO).where = where;
						_model.transactions.lastQuery.dispatch();
						
					break;
				}
			}

			private function toDateChange(event:Event):void 
			{
				moduleGrid.clearSearch.visible = true;

				switch(_model.transactions.lastQuery.type){
					case TransactionsEvent.TRANSACTIONS_RECORDS:

						var where : Object = buildWhere( 4, getToDateWhere() );
						
						RecordsVO(_model.transactions.lastQuery.params.recordsVO).where = where;
						_model.transactions.lastQuery.dispatch();
						
 					break;
				}
			}

			private function listClickHandler():void 
			{
				_model.transactions.sidRecord = false;
				_model.transactions.details = moduleGrid.clickedItem;
			}
			
			private function handleExportClick( event : Event ) : void {
				
				var where : Object;
				try {
					where = RecordsVO(_model.transactions.lastQuery.params.recordsVO).where;
				}
				catch( e : Error ) {
					where = buildWhere( 2, null );
				}
				
				new TransactionsEvent( 
					TransactionsEvent.TRANSACTIONS_EXPORT, 
						{ 
							recordsVO : new RecordsVO (
								'transactions(pledge_id(contact_id),created_by_id<fname:lname:_fid>)',
								where,
								'transactions.created_on DESC'
							)
						}
					).dispatch();	
			}

			private function date_labelFunc(item:Object, column:DataGridColumn):String 
			{
				return dateFormat.format( EDateUtil.timestampToLocalDate( item[column.dataField] ) );
			}

			private function amountLabel(item:Object, column:DataGridColumn):String
			{
				return currencyFormat.format(item.amount);
			}

			private function numberSort(itemA:Object, itemB:Object):int
			{
				return ObjectUtil.numericCompare(itemA.amount, itemB.amount);
			}

			private function timeSort(itemA:Object, itemB:Object):int
			{
				return ObjectUtil.numericCompare(itemA.created_on, itemB.created_on);
			}

		]]>
	</mx:Script>
</common:LabeledContainer>
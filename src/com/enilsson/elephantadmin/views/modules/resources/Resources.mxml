<?xml version="1.0" encoding="utf-8"?>
<common:LabeledContainer
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:common="com.enilsson.elephantadmin.views.common.*" 
	xmlns:resources="com.enilsson.elephantadmin.views.modules.resources.*"
	verticalScrollPolicy="off" horizontalScrollPolicy="off"
	label="{_model.viewStateNames.getItemAt(moduleID)}"
	>

	<mx:Script>
		<![CDATA[
			import com.enilsson.elephantadmin.models.Icons;
			import mx.events.CloseEvent;
			import mx.controls.Alert;
			import com.enilsson.utils.eNilssonUtils;
			import mx.events.ResourceEvent;
			import com.enilsson.elephantadmin.vo.RecordVO;
			import org.osflash.thunderbolt.Logger;
			import mx.utils.ObjectUtil;
			import com.enilsson.elephantadmin.vo.RecordsVO;
			import com.enilsson.elephantadmin.vo.SearchVO;
			import com.enilsson.elephantadmin.events.modules.ResourcesEvent;
			import mx.collections.ArrayCollection;
			import com.enilsson.elephantadmin.models.EAModelLocator;
			import com.enilsson.graphics.enDropShadows;
			
			[Bindable] private var _model:EAModelLocator = EAModelLocator.getInstance();

			override protected function init():void
			{
				super.init();

				// get default list
				clearSearch();
				// reset the data changed flag
				_model.dataChanged = false;
			}
			
			/**
			 * Handle the search request
			 */

			private function searchStart(event:Event):void
			{
				if(resourcesList.searchTerm != ""){
				resourcesList.clearSearch.visible = true;

					if(resourcesList.searchOption.data == 0)
					{
						new ResourcesEvent(
							ResourcesEvent.RESOURCES_SEARCH,  {
								'searchVO' : new SearchVO(
									'resources',
									resourcesList.searchTerm,
									null,
									0,
									_model.itemsPerPage
								)
							}
						).dispatch();
					}
					else
					{
						var searchField:String = resourcesList.searchOption.data;
	
						var whereObj:Object =  new Object();
							whereObj['statement'] = '(1)';
							whereObj[1] = { 
								'what' : searchField,
								'val' : "%"+resourcesList.searchTerm+"%",
								'op' : 'LIKE'
							};
		
						new ResourcesEvent( 
							ResourcesEvent.RESOURCES_RECORDS, { 
								'recordsVO' : new RecordsVO( 
									'resources(attachment_id<filename>)', 
									whereObj, 
									'resources.title ASC', 
									_model.resources.currPage, 
									_model.itemsPerPage, 
									'P' 
								) 
							} 
						).dispatch();
					}
				}
			}

			private function indexSearchStart(event:Event):void
			{
				resourcesList.clearSearch.visible = true;
				var whereObj:Object =  new Object();
					whereObj['statement'] = '(1)';
					whereObj[1] = { 
						'what' : 'resources.title',
						'val' : resourcesList.indexBar.searchLetter+"%",
						'op' : 'LIKE'
					};
				new ResourcesEvent( 
					ResourcesEvent.RESOURCES_RECORDS, { 
						'recordsVO' : new RecordsVO( 
							'resources(attachment_id<filename>)', 
							whereObj, 
							'resources.title ASC', 
							_model.resources.currPage, 
							_model.itemsPerPage, 
							'P' 
						) 
					} 
				).dispatch();
			}

			/**
			 * Run a new data event when the clear search event is run
			 */
			private function clearSearch():void
			{
				resourcesList.clearSearch.visible = false;
				_model.resources.currPage = 0;
				new ResourcesEvent( 
					ResourcesEvent.RESOURCES_RECORDS, { 
						'recordsVO' : new RecordsVO( 
							'resources(attachment_id<filename>)', 
							null, 
							'resources.id DESC', 
							_model.resources.currPage, 
							_model.itemsPerPage, 
							'P' 
						) 
					} 
				).dispatch();
			}
			
			/**
			 * Run a new data event when the pagination buttons are clicked
			 */
			private function newPageAction(event:Event):void
			{
				_model.resources.currPage = resourcesList.selectedPage * _model.itemsPerPage;
				switch(_model.resources.lastQuery.type){
					case ResourcesEvent.RESOURCES_RECORDS:
						RecordsVO(_model.resources.lastQuery.params.recordsVO).iFrom = _model.resources.currPage;
						_model.resources.lastQuery.dispatch();
 						break;
					
					case ResourcesEvent.RESOURCES_SEARCH:
						SearchVO(_model.resources.lastQuery.params.searchVO).iFrom = _model.resources.currPage;
						_model.resources.lastQuery.dispatch();
						break;
				}							
			}			
			
			/**
			 * Run an action when the list items are clicked
			 */
			private function listClickHandler():void
			{
				_model.resources.selectedIndex = resourcesList.selectedIndex;
				_model.resources.formRecord = resourcesList.clickedItem;
				_model.resources.addingRecord = false;
			}
			
			/**
			 * Handle the action list clicks
			 */
			private function addClickHandler(event:Event):void
			{
				_model.resources.formRecord = new Object();
				_model.resources.addingRecord = true;
			}

			private function exportClickHandler(event:Event):void
			{
				var params:Object = new Object();
				params.limit = resourcesList.itemsTotal;

				new ResourcesEvent( 
					ResourcesEvent.RESOURCES_EXPORT, {
						'recordVO' :  new RecordVO (
							'resources(attachment_id)',
							0,
							params
						)
					}
				).dispatch();
			}
			
			/**
			 * Run the upsert event to record the data in the form
			 */
			private function upsertRecord(event:Event):void
			{
				if( resources_form.isValid === false) return;

				if(_model.debug)
				{
					Logger.info('Form Record', _model.resources.formRecord);
					Logger.info('FormVariables', resources_form.formVariables);
				}
				
				var fv:Object = resources_form.formVariables;
				
				// if the resource is an HTML link then just upsert
				if( fv.filetype == 'html' )
					executeUpsert();
				else
				{
					if( _model.resources.fileChanged == true )
						upload();
					// if the file is different from the existing one then upload the new file
					else
						executeUpsert();
				}						
			}
	
			/**
			 * Handle the actual upsert action
			 */
			private function executeUpsert():void
			{
				// set the form variables and assign the appropriate RLAC
				var fv:Object = resources_form.formVariables;
				fv['mod_e_read'] = resources_options.resources_access.visibilityType == 'all' ? 1 : 0;
				fv['mod_g_read'] = resources_options.resources_access.visibilityType == 'group' ? 1 : 0;
				fv['mod_g_write'] = resources_options.resources_access.visibilityType == 'group' ? 1 : 0;
				fv['mod_group_id'] = resources_options.resources_access.visibilityType == 'group' ? resources_options.resources_access.groupID : 0;
				
				if(_model.debug) Logger.info('Resources Form Variables', ObjectUtil.toString(fv));

				new ResourcesEvent( 
					ResourcesEvent.RESOURCES_UPSERT, {
						'recordVO' :  new RecordVO (
							'resources',
							0,
							fv
						)
					}
				).dispatch();
			}
			
			/**
			 * Upload the file listed to S3
			 */			
			private function upload():void
			{
				// make sure there are no spaces in the filelink name
				// resources_form.formVariables.link = eNilssonUtils.niceFileName(resources_form.formVariables.link);
			
				var phpSessID:String = _model.session.data.php_session_id;
				var gURL:String = _model.gatewayURL.match(/key=(.*)/)[0].toString().replace("key=","");
				
				// create the URLRequest
				var request:URLRequest = new URLRequest();
				request.url = _model.gatewayBaseURL+'upload_attachment.php?PHPSESSID=' + phpSessID + '&key=' + gURL;
				if(_model.resources.formRecord.attachment_id)
					request.url += '&id=' + _model.resources.formRecord.attachment_id.id;
				request.method = URLRequestMethod.POST;
				
				if(_model.debug) Logger.info('Request URL', request.url);
			    
			    // start the upload process
			    _model.resources.fileRef.addEventListener(Event.OPEN, function(e:Event):void {
			    	if(_model.debug) Logger.info('Open Upload', ObjectUtil.toString(e.currentTarget));
			    	resources_form.showProgress = true;
			    });
			    
			    // capture the data completion
			    _model.resources.fileRef.addEventListener(DataEvent.UPLOAD_COMPLETE_DATA, function(e:DataEvent):void {
			    	if(_model.debug) Logger.info('Complete Data', ObjectUtil.toString(e));
			    	resources_form.showProgress = false;
			    	if(isNaN(int(e.data)))
			    	{
			    		Logger.info('Error', ObjectUtil.toString(e));
			    	}
			    	else
			    	{
			    		resources_form.formVariables.attachment_id = e.data;
				    	executeUpsert();
			    	}
			    });
			    
			    // register the HTTP failure
			    _model.resources.fileRef.addEventListener(HTTPStatusEvent.HTTP_STATUS, function(e:HTTPStatusEvent):void{
			    	if(_model.debug) Logger.info('HTTP Status', ObjectUtil.toString(e));
			    });
			    
			    // handle an IO error
			    _model.resources.fileRef.addEventListener(IOErrorEvent.IO_ERROR, function(e:IOErrorEvent):void {
			    	if(_model.debug) Logger.info('IOError', ObjectUtil.toString(e));
			    	resources_form.showProgress = false;
			    });				    
			    
			    // measure the progress of the upload
			    _model.resources.fileRef.addEventListener(ProgressEvent.PROGRESS, function(e:ProgressEvent):void {
			    	var numPerc:int = Math.round((Number(e.bytesLoaded) / Number(e.bytesTotal)) * 100);
			    	resources_form.progress = numPerc;
			    });
			    
			    // handle the completion of the upload
				_model.resources.fileRef.addEventListener(Event.COMPLETE, function(e:Event):void {
			    	if(_model.debug) Logger.info('Complete', ObjectUtil.toString(e));
			    	resources_form.showProgress = false;
			    	_model.email_attachments.fileRef = new FileReference();
			    });

                _model.resources.fileRef.upload(request, 'Filedata', false);
			}

			private function download(event:Event):void
			{
				var phpSessID:String = _model.session.data.php_session_id;
				var gURL:String = _model.gatewayURL.match(/key=(.*)/)[0].toString().replace("key=","");
				
				// create the URLRequest
				var request:URLRequest = new URLRequest();
				request.url = _model.gatewayBaseURL+'view_attachment.php?PHPSESSID=' + phpSessID + '&key=' + gURL + "&id=" + _model.resources.formRecord.attachment_id.id + "&refresh=" + new Date().getTime();
				request.method = URLRequestMethod.GET;
				
				if(_model.debug) Logger.info('Request URL', request.url);
				navigateToURL(request,"_parent");

			}

			/**
			 * Run the delete event
			 */
			private function deleteRecord(event:Event):void
			{
				if (resourcesList.clickedItem == null) {
					return;
				}
				
				if(_model.debug) Logger.info('Deleting Resources', resourcesList.clickedItem.id);

				var confirmBox:Alert = Alert.show('Are you sure you want to delete this item?', 'Delete', Alert.YES | Alert.NO,this,function(eventObj:CloseEvent):void {
					if (eventObj.detail==Alert.YES) 
					{
						new ResourcesEvent( 
							ResourcesEvent.RESOURCES_DELETE, {
								'recordVO' :  new RecordVO (
									'resources',
									resourcesList.clickedItem.id
								)
							}
						).dispatch();
					}
				},null,Alert.NO); 
				confirmBox.width = 330;
			}

		]]>
	</mx:Script>
	
	<mx:HDividedBox horizontalGap="10" width="100%" height="100%">
	
		<common:ModuleList id="resourcesList" 
			height="100%"
			width="100%"
			dataProvider="{_model.resources.records}"
			searchComboList="{_model.resources.searchBoxCombo}"
			itemRenderer="com.enilsson.elephantadmin.views.modules.resources.Resources_ModuleList"
			listItemClick="listClickHandler()" 
			searchStart="searchStart(event)" 
			indexSearch="indexSearchStart(event)"
			clearSearch="clearSearch()" 
			newPage="newPageAction(event)" 
			selectedPage="{_model.resources.currPage / _model.itemsPerPage}" 
			selectedIndex="{_model.resources.selectedIndex}" 
			itemsPerPage="{_model.itemsPerPage}" 
			itemsTotal="{_model.resources.totalRecords}"
			/>
		
		<resources:Resources_Form id="resources_form"
			width="255" minWidth="255" maxWidth="255" height="100%"
			upsertRecord="upsertRecord(event)"
			deleteRecord="deleteRecord(event)"
			downloadFile="download(event)"/>
			
		<mx:VBox width="350" minWidth="300" maxWidth="350" height="100%">
			<mx:HBox horizontalAlign="right" width="100%" horizontalScrollPolicy="off"
				height="25">
				<mx:Button id="addItemBtn" 
					label="ADD ITEM" 
					click="addClickHandler(event)" 
					icon="{Icons.ADD}" 
					width="100" height="25" 
					styleName="actionsList" />
				<common:RestrictedExportBtn id="exportBtn" 
					label="EXPORT" 
					click="exportClickHandler(event)" 
					icon="{Icons.EXPORT}" 
					width="100" height="25" 
					styleName="actionsList" />
			</mx:HBox>
			<resources:Resources_Options id="resources_options"
				width="100%" height="100%"/>
		</mx:VBox>
	</mx:HDividedBox>
</common:LabeledContainer>
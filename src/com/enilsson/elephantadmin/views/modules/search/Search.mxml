<?xml version="1.0" encoding="utf-8"?>
<common:LabeledContainer
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:common="com.enilsson.elephantadmin.views.common.*" 
	xmlns:search="com.enilsson.elephantadmin.views.modules.search.*">

	<mx:VBox id="searchBoxContainer" width="100%" height="{this.height}">
		<mx:Repeater id="rep" 
			dataProvider="{SearchViewClass.SEARCH_OPTIONS}" 
			startingIndex="1">
				<search:SearchCategoryBox id="boxes" width="100%" 
					creationComplete="handleBoxCreationComplete(event)" />
		</mx:Repeater>
	</mx:VBox>
	
	<mx:Script>
		<![CDATA[
			import com.enilsson.graphics.enDropShadows;
			import com.enilsson.elephantadmin.models.viewclasses.SearchViewClass;
			import mx.utils.ObjectUtil;
			import mx.collections.ArrayCollection;
		
			import com.enilsson.elephantadmin.models.EAModelLocator;
			
			import org.osflash.thunderbolt.Logger;
			
			[Bindable] private var _model:EAModelLocator = EAModelLocator.getInstance();
			
			
			[Bindable]			
			public function set query( value : String ) : void
			{
				_query = value;
				
				for each(var box : SearchCategoryBox in boxes ) {
					box.searchQuery = getQueryForBox(box);
				}
			}
			public function get query() : String { return _query; }
			private var _query : String;
			
			
			[Bindable]
			public function set searchOption( value : String ) : void
			{
				_searchOption = value;
				query = null;	
			}
			public function get searchOption() : String { return _searchOption; }
			private var _searchOption : String;
			
			
			private function handleBoxCreationComplete( event : Event ) : void 
			{
				var box : SearchCategoryBox = event.currentTarget as SearchCategoryBox;
				box.data = rep.dataProvider[box.repeaterIndex];
				box.searchQuery = getQueryForBox(box);
			}
									
			private function getQueryForBox( box : SearchCategoryBox  ) : String
			{
				if( _query && box ) 
				{					
					var t : String = box.table;
					
					if( t == searchOption || searchOption == SearchViewClass.EVERYTHING )
					{	return _query;	}
				}
				
				return null;
			}

		]]>
	</mx:Script>
	
</common:LabeledContainer>

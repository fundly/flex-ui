<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:common="com.enilsson.elephantadmin.views.common.*"
	label="MAXOUT"
	enabled="{_presentationModel.maxOutTabEnabled}"
	implements="com.enilsson.elephantadmin.interfaces.IOptionView"	
	width="100%" height="100%">
	
	<mx:Style>
		.maxOutError {
			color: #333333;
			horizontalCenter: 0;
			verticalCenter: 0;	
		}
		.maxOutTitle {
			fontSize: 18;
			color: #333333;	
		}
		.maxOutDesc {
			fontSize: 10;
			color: #333333;
		}
		.maxOutCType {
			fontSize: 12;
			fontWeight: bold;
			color: #666666;
		}
		.maxOutAmt {
			fontSize: 10;
			color: #666666;
		}
	</mx:Style>
	
	
	<mx:Binding source="_presentationModel.maxOut" destination="this.buildMaxOut" />
	
	<mx:Script>
		<![CDATA[
			import mx.controls.HRule;
			import mx.containers.VBox;
			import org.osflash.thunderbolt.Logger;
			import com.enilsson.elephantadmin.views.modules.contacts.renderers.Contacts_MaxOutRenderer;
			import mx.collections.ArrayCollection;
			import mx.controls.Text;
			import com.enilsson.elephantadmin.views.modules.contacts.model.ContactsModel;
			import com.enilsson.elephantadmin.interfaces.IRecordModel;

			[Bindable] private var _presentationModel:ContactsModel;
			public function set presentationModel ( value:IRecordModel ):void
			{
				_presentationModel = ContactsModel(value);
			}
			public function get presentationModel():IRecordModel
			{
				return _presentationModel;
			}
			
			public function set buildMaxOut ( value:Object ):void
			{
				this.removeAllChildren();
				
				if ( _presentationModel.maxOut.found_rows == 0 )
				{
					var text:Text = new Text();
					text.styleName = 'maxOutError';
					text.text = 'There are no max out records for this contact!';
										
					addChild( text );	
				}				
//				else if ( _presentationModel.maxOut.max_fund_contrib_per_year )
//				{
//					Logger.info('MaxOut', 'Maxout By Year');	
//				}
				else
				{
					var vbox:VBox = new VBox();
					vbox.percentWidth = 90;
					vbox.percentHeight = 90;
					vbox.setStyle( 'horizontalAlign', 'middle' );
					vbox.setStyle( 'horizontalCenter', 0 );
					vbox.setStyle( 'verticalGap', 12 );
					
					var desc:Text = new Text();
					desc.percentWidth = 100;
					desc.text = 'Below are the contributions, towards the maximum allowed, that have been made by all matches to this contact. They are listed by each fund and contribution type.';
					desc.styleName = 'maxOutDesc';
					vbox.addChild( desc );
					
					var hr:HRule = new HRule();
					hr.percentWidth = 100;
					vbox.addChild( hr );
					
					for ( var i:String in _presentationModel.maxOut.max_fund_contrib )
					{
						var title:Text = new Text();
						title.text = i;
						title.styleName = 'maxOutTitle';
						
						var limits:Object = _presentationModel.maxOut.max_fund_contrib[i];
						var records:ArrayCollection = new ArrayCollection();
						
						for each ( var item:Object in _presentationModel.maxOut.contacts_match_max )
						{
							if ( item.fund_type == i )
							{
								item['limit'] = limits[item.contribution_type];
								item['percentage'] = Math.round( Number(item.contrib_total) / item.limit * 100 );
								records.addItem ( item );
							}
						}
						
						if ( records.length > 0 )
						{
							vbox.addChild( title );
							vbox.addChild( buildSingleMaxOut ( i, records ) );	
			
							hr = new HRule();
							hr.percentWidth = 100;
							vbox.addChild( hr );
						}
					}
						
					addChild ( vbox );
				}
			}
			
			private function buildSingleMaxOut ( fundType:String, records:ArrayCollection ):VBox
			{
				var vbox:VBox = new VBox();
				var contributorTypes:Array = [];
				
				for ( var i:String in _presentationModel.maxOut.max_fund_contrib[fundType] )
					contributorTypes.push( i );
				
				for each ( var item:Object in records )
				{
					if ( contributorTypes.indexOf( item.contribution_type ) != -1 )
					{
						var maxOutItem:Contacts_MaxOutRenderer = new Contacts_MaxOutRenderer();
						maxOutItem.percentWidth = 100;
						maxOutItem.data = item;						
						
						vbox.addChild( maxOutItem );
					}
				}	
				
				return vbox;
			}
		]]>
	</mx:Script>
	
</mx:Canvas>
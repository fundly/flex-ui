<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:struktorForm="com.enilsson.utils.struktorForm.*"
	xmlns:graphics="com.enilsson.graphics.*"
	show="init()"
	hide="hideAction()"
	width="100%" 
	verticalScrollPolicy="off" 
	horizontalScrollPolicy="off"
	xmlns:ns="com.enilsson.elephanttrakker.views.modules.call_logging.*">

	<struktorForm:StruktorForm id="contactDetails" styleName="processingForm"
		width="100%" maxHeight="240" left="0" top="6"
		selectedGroups="{[1]}" 
		layoutProvider="{_model.struktorLayout.pledges}"
		dataProvider="{_model.call_logging.initialContactData}"
		debugMode="true" validateOnKeyPress="false" 
		numColumns="2"
		isValidChanged="this.isValid = true;" 
		formBuildComplete="formBuildCompleteAction(event)"			
		groupLabels="false"
		/>

	<mx:Binding source="contactDetails.formVariables" destination="_model.call_logging.contactData" />
	<mx:Binding source="contactDetails.fieldNames" destination="_model.call_logging.contactsFields" />
	<mx:Binding source="contactDetails.invalidFields" destination="_model.call_logging.contactsErrors" />
	
	<mx:Metadata>
		[Event(name="isValidChanged", type="flash.events.Event")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import com.enilsson.elephanttrakker.events.session.PingEvent;
			import com.enilsson.controls.InfoBtn;
			import mx.managers.ToolTipManager;
			import mx.core.IToolTip;
			import mx.controls.TextInput;
			import mx.utils.ObjectUtil;
			import org.osflash.thunderbolt.Logger;
			import com.enilsson.elephanttrakker.models.ETModelLocator;
			
			[Bindable] private var _model:ETModelLocator = ETModelLocator.getInstance();
			[Bindable] private var _isValid:Boolean = false;
			[Bindable] private var _fv:Object = new Object();
			
			private var firstField:*;	
			private var lastField:*;		
		
			/**
			 * Public functions to announce that this tab is valid (or not)
			 */
			public function set isValid(value:Boolean):void
			{
				if(contactDetails.numChildren == 0)
				{
					_model.call_logging.contactDetailsValid = false;
					return;
				}
				
				_isValid = contactDetails.isValid;				
				_model.call_logging.contactDetailsValid = _isValid;
				_model.call_logging.workspaceValid = _isValid;
				
				if(_model.debug) Logger.info('ContactDetails Status', _model.call_logging.contactDetailsValid);
				
				dispatchEvent( new Event('isValidChanged') );
			}
			
			[Bindable(event="isValidChanged")]
			public function get isValid():Boolean
			{
				return _isValid;
			}
						
			/**
			 * Initialisation routine
			 */
			private function init():void
			{
				// ping the server to make sure that all is well
				new PingEvent().dispatch();

 				if(_model.call_logging.vindex >= _model.call_logging.prevVIndex)
					firstField.setFocus();
				else 
				{
					if(_model.call_logging.tabBackward)
					{
						lastField.setFocus();
						_model.call_logging.tabBackward = false;
					}
				}
			}
			
			/**
			 * Hide any error tips when the form is hidden (ie the user moves to a new tab)
			 */
			private function hideAction():void
			{
				var curTT:IToolTip = ToolTipManager.currentToolTip;
				
				if(curTT)
					ToolTipManager.destroyToolTip(curTT);
			}
		
			/**
			 * Complete some actions once the Struktor forms complete building
			 */
			private function formBuildCompleteAction(event:Event):void
			{
				if(_model.debug) 
				Logger.info('Contact Details: Form Build Complete');

				// get the first field in the form
				firstField = contactDetails.firstField;
				
				// get the last field in the form
				lastField = contactDetails.lastField;
				
				// assign an event handler to the last field to cope with tabbing to the next page						
				lastField.addEventListener(FocusEvent.KEY_FOCUS_CHANGE, function(evt:FocusEvent):void {
					if (!evt.shiftKey && evt.keyCode == 9) {
						_model.call_logging.vindex++;
						evt.preventDefault();
					}
				});	
															
				// validate the form if the action is edit
				if(_model.call_logging.formAction == 'edit')
					this.isValid = true;
				
				// send the form variables to the model to begin the process
				_model.call_logging.contactData = contactDetails.formVariables;
			}

		]]>
	</mx:Script>
				
</mx:Canvas>